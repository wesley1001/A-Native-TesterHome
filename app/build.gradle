apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 9
def versionBuild = 1 // bump for dogfood builds, public betas, etc.

//Properties properties = new Properties()
// local.properties file in the root director
//properties.load(project.rootProject.file('local.properties').newDataInputStream())

//def keyStorePassWord = properties.getProperty('KEYSTORE_PASSWORD')
//def keyPassWord = properties.getProperty('KEY_PASSWORD')


android {
    signingConfigs {
        SigningConfig {
            keyAlias 'TesterHome'
            storeFile file('keystore.jks')
            storePassword KEYSTORE_PASSWORD
            keyPassword KEY_PASSWORD
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "com.testerhome.nativeandroid"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }
    buildTypes {
        release {
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            signingConfig signingConfigs.SigningConfig
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "3g"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:customtabs:25.0.1'

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.facebook.fresco:fresco:0.10.0'
    compile 'com.facebook.fresco:imagepipeline-okhttp3:0.10.0'

    compile 'com.squareup.okhttp3:okhttp:3.2.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    // RxJava itself
    compile 'io.reactivex:rxjava:1.1.5'
    // And ProGuard rules for RxJava!
    compile 'com.artemzin.rxjava:proguard-rules:1.1.5.0'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'zhou.widget:richtext:1.0.4'
    compile 'org.tautua.markdownpapers:markdownpapers-core:1.4.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'

    compile 'org.greenrobot:greendao:2.2.0'
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    compile 'com.google.android.gms:play-services-analytics:10.0.1'
    compile project(':swipebackhelper')
    compile 'com.tonicartos:superslim:0.4.13'

    compile 'com.tencent.bugly:crashreport_upgrade:latest.release'
}

